Import('env')
import sys

files = [
	'BootManager.cpp',
	'Config.cpp',
	'FileSearch.cpp',
	'Frame.cpp',
	'GameListCtrl.cpp',
	'Globals.cpp',
	'ISOFile.cpp',
	'MemcardManager.cpp',
	'MemoryCards/GCMemcard.cpp',
	'PluginManager.cpp',
	'PluginOptions.cpp',
	'stdafx.cpp',
        'cmdline.c',
	]
libs = [
	'debwx', 'core', 'lzo', 'discio', 'bdisasm', 'videocommon', 'common', 'z'
	]

wxenv = env.Copy()
wxenv.Append(
	CXXFLAGS = ' ' + ' '.join([
		'`wx-config --cppflags`',
		'-DUSE_XPM_BITMAPS',
		'-DwxNEEDS_CHARPP',
		'`sdl-config --cflags`',
		]),
	LINKFLAGS = ' ' + ' '.join([
		'-L/usr/local/lib',
		'-pthread',
		'`wx-config --libs`',
		'`sdl-config --libs`'
		])
	)

if sys.platform == 'darwin':
	exeGUI = '../../../../Binary/mac/Dolphin.app/Contents/MacOS/Dolphin'
	exeNoGUI = '../../../../Binary/mac/DolphinNoGUI'

	icon = 'Dolphin'
	version = 'svn'
	wxenv.Plist(
		'../../../../Binary/mac/Dolphin.app/Contents/Info.plist',
		Value(dict(
			CFAppleHelpAnchor = 'index',
			CFBundleExecutable = 'Dolphin',
			CFBundleGetInfoHTML = 'Dolphin ' + version,
			CFBundleIconFile = icon,
			CFBundleIdentifier = 'com.dolphin-emu.dolphin',
			CFBundleName = 'Dolphin',
			CFBundlePackageType = 'APPL',
			CFBundleShortVersionString = version,
			CFBundleSignature = 'dlfn',
			CFBundleVersion = version,
			LSRequiresCarbon = True,
			NSPrefPaneIconFile = icon,
			NSPrefPaneIconLabel = 'Dolphin',
			))
		)
else:
	exeGUI = '../../../../Binary/linux/Dolphin'
	exeNoGUI = '../../../../Binary/linux/DolphinNoGUI'

objects = [ wxenv.Object(srcFile) for srcFile in files ]
wxenv.Program(exeGUI, objects + [ 'Main.cpp' ], LIBS = libs)
wxenv.Program(exeNoGUI, objects + [ 'MainNoGUI.cpp' ], LIBS = libs)
