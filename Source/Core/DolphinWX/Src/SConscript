# -*- python -*- 

Import('env')
import sys

files = [
	'BootManager.cpp',
	'Config.cpp',
	'FileSearch.cpp',
	'Globals.cpp',
	'ISOFile.cpp',
	'MemoryCards/GCMemcard.cpp',
	'stdafx.cpp',
	'cmdline.c',
	]
libs = [
	'core', 'minilzo', 'discio', 'bdisasm', 'videocommon', 'common', 'z'
	]
if not env['nowx']:
	files += ['ConfigMain.cpp', 'FilesystemViewer.cpp', 'Frame.cpp', 'GameListCtrl.cpp', 'MemcardManager.cpp', 'PluginManager.cpp',]
	libs += ['debwx', ]

wxenv = env.Clone()
wxenv.Append(
	CPPDEFINES = [
		'USE_XPM_BITMAPS',
		'wxNEEDS_CHARPP',
		],
	LINKFLAGS = [
		'-lpthread',
		],
	LIBS = libs
	)

if sys.platform == 'darwin':
	exeGUI = '../../../../Binary/mac/Dolphin.app/Contents/MacOS/Dolphin'
	exeNoGUI = '../../../../Binary/mac/DolphinNoGUI'

	icon = 'Dolphin'
	version = 'svn'
	wxenv.Plist(
		'../../../../Binary/mac/Dolphin.app/Contents/Info.plist',
		Value(dict(
			CFAppleHelpAnchor = 'index',
			CFBundleExecutable = 'Dolphin',
			CFBundleGetInfoHTML = 'Dolphin ' + version,
			CFBundleIconFile = icon,
			CFBundleIdentifier = 'com.dolphin-emu.dolphin',
			CFBundleName = 'Dolphin',
			CFBundlePackageType = 'APPL',
			CFBundleShortVersionString = version,
			CFBundleSignature = 'dlfn',
			CFBundleVersion = version,
			LSRequiresCarbon = True,
			NSPrefPaneIconFile = icon,
			NSPrefPaneIconLabel = 'Dolphin',
			))
		)
else:
	exeGUI = '../../../../Binary/linux/Dolphin'
	exeNoGUI = '../../../../Binary/linux/DolphinNoGUI'

objects = [ wxenv.Object(srcFile) for srcFile in files ]
if not env['nowx']:
	wxenv.Program(exeGUI, objects + [ 'Main.cpp' ])	
wxenv.Program(exeNoGUI, objects + [ 'MainNoGUI.cpp' ])
