# -*- python -*- 

Import('env')
import sys

files = [
	'BootManager.cpp',
	'Config.cpp',
	'cmdline.c',
	]

if not env['osx64']:
    files += 	[
		'ConfigMain.cpp',
       		'FileSearch.cpp',
       		'FilesystemViewer.cpp',
       		'Frame.cpp',
       		'GameListCtrl.cpp',
       		'Globals.cpp',
       		'ISOFile.cpp',
       		'MemcardManager.cpp',
       		'MemoryCards/GCMemcard.cpp',
       		'PluginManager.cpp',
       		'stdafx.cpp',
		]


libs = [
	'core', 'minilzo', 'discio', 'bdisasm', 'videocommon', 'common', 'z'
	]

wxenv = env.Clone()
if not wxenv['osx64']:
    libs = [ 'debwx' ] + libs
    wxenv.Append(
	CPPDEFINES = [
		'USE_XPM_BITMAPS',
		'wxNEEDS_CHARPP',
		],
	LINKFLAGS = [
		'-pthread',
		],
	LIBS = libs
	)
else:
    wxenv.Append(
	CPPDEFINES = [
		'USE_XPM_BITMAPS',
		'wxNEEDS_CHARPP',
		],
	LINKFLAGS = [
		'-pthread',
		'-arch',
		'x86_64'
		],
	LIBS = libs
	)

if sys.platform == 'darwin':
	exeGUI = '../../../../Binary/mac/Dolphin.app/Contents/MacOS/Dolphin'
	exeNoGUI = '../../../../Binary/mac/DolphinNoGUI'

	icon = 'Dolphin'
	version = 'svn'
	wxenv.Plist(
		'../../../../Binary/mac/Dolphin.app/Contents/Info.plist',
		Value(dict(
			CFAppleHelpAnchor = 'index',
			CFBundleExecutable = 'Dolphin',
			CFBundleGetInfoHTML = 'Dolphin ' + version,
			CFBundleIconFile = icon,
			CFBundleIdentifier = 'com.dolphin-emu.dolphin',
			CFBundleName = 'Dolphin',
			CFBundlePackageType = 'APPL',
			CFBundleShortVersionString = version,
			CFBundleSignature = 'dlfn',
			CFBundleVersion = version,
			LSRequiresCarbon = True,
			NSPrefPaneIconFile = icon,
			NSPrefPaneIconLabel = 'Dolphin',
			))
		)
else:
	exeGUI = '../../../../Binary/linux/Dolphin'
	exeNoGUI = '../../../../Binary/linux/DolphinNoGUI'

objects = [ wxenv.Object(srcFile) for srcFile in files ]
if not wxenv['osx64']:
    wxenv.Program(exeGUI, objects + [ 'Main.cpp' ])
wxenv.Program(exeNoGUI, objects + [ 'MainNoGUI.cpp' ])
